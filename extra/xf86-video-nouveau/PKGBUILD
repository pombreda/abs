# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: buddabrod <buddabrod@gmail.com>

pkgname=xf86-video-nouveau
_gitdate=20100517
pkgver=0.0.16_git${_gitdate} # see configure.ac
pkgrel=1
pkgdesc="Open Source 3D acceleration driver for nVidia cards (experimental)"
arch=('i686' 'x86_64')
url="http://nouveau.freedesktop.org/wiki/"
license=('GPL') #and MIT, not yet a license file, see http://nouveau.freedesktop.org/wiki/FAQ#head-09f75d03eb30011c754038a3893119a70745de4e
depends=('libdrm>=2.4.19' 'udev')
optdepends=('nouveau-dri:	highly experimental gallium3d features')
makedepends=('pkgconfig' 'xorg-server>=1.8.0' 'mesa>=7.8.1' 'glproto>=1.4.11' 'xorg-util-macros' 'xf86driproto' 'autoconf' 'nouveau-dri>=7.8.1')
conflicts=('xorg-server<1.8.0')
options=('!libtool') # 'force'
install=${pkgname}.install
source=(ftp://ftp.archlinux.org/other/$pkgname/xf86-video-nouveau-${_gitdate}.tar.bz2)
md5sums=('787f5cbba1f14f4ffcafbb00516abdad')

# source PKGBUILD && mksource
mksource() {
	mkdir /tmp/${pkgname}-${_gitdate}
	pushd /tmp/${pkgname}-${_gitdate}
	git clone -v --depth 1 git://anongit.freedesktop.org/nouveau/xf86-video-nouveau
	cd xf86-video-nouveau
	git archive --prefix=xf86-video-nouveau-${_gitdate}/ --format=tar HEAD | bzip2 > /tmp/${pkgname}-${_gitdate}/${pkgname}-${_gitdate}.tar.bz2
	popd
}

build() {
  cd ${srcdir}/xf86-video-nouveau-${_gitdate}
  ./autogen.sh --prefix=/usr || return 1
  make || return 1
}

package() {
  cd ${srcdir}/xf86-video-nouveau-${_gitdate}
  make DESTDIR=${pkgdir} install || return 1
}
