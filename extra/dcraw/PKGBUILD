# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

# Note for rebuilders:
# dcraw.c  is released as pure C file, not as a package with a changing version
# number in the name. The name of the file to be downloaded is always the same!
# So if you are a bad boy (Santa knows everything!) and build as root, this file
# will be saved in /var/cache/pacman/src . Therefore, rebuilding a later version
# likely will fail due to an MD5 error. If so, delete the files from the cache
# to force a new download.

pkgname=dcraw
pkgver=1.437
pkgrel=1
pkgdesc="A command line based converter for digicams raw output"
arch=('i686' 'x86_64')
url="http://www.cybercom.net/~dcoffin/dcraw/"
license=('custom')
depends=('lcms')
source=(http://www.cybercom.net/~dcoffin/${pkgname}/${pkgname}.c
        http://www.cybercom.net/~dcoffin/${pkgname}/${pkgname}.1 
        license.txt)
md5sums=('55814edbb3122dcad9def7f4b614227f'
         '6ba2a797c4ad698589f83171d6b36396'
         '110b4604a6dacd5537339c4082e9e12a')
sha1sums=('5446bd5c7bcafb37b7fbea885666f3e8b9a4dbfe'
          '0902f48378a2ae0a58f8c8d71560f7f15d21fb26'
          '502c8f0ac7434d48f5dcb2eefe7599627cb69454')


build() {
  cd "${srcdir}"
  gcc -o dcraw ${CFLAGS} dcraw.c -lm -ljpeg -llcms
}

package() {
  cd "${srcdir}"
  install -Dm755  ${pkgname} "${pkgdir}"/usr/bin/${pkgname}
  
  # install a manpage, provided separately on authors homepage
  install -Dm644 ${pkgname}.1 "${pkgdir}"/usr/share/man/man1/${pkgname}.1
  
  install -Dm644 license.txt "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}
