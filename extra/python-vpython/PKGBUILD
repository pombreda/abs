# $Id$
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Comete <la_comete@tiscali.fr>

pkgname=python-vpython
pkgver=5.32
pkgrel=1
pkgdesc="A Python module that offers real-time 3D output"
arch=('i686' 'x86_64')
url="http://www.vpython.org"
depends=('gtkglextmm' 'libglademm' 'python' 'python-numpy' 'boost-libs' 'tk')
makedepends=('pkg-config' 'boost')
license=('custom')
options=(!libtool)
source=(http://www.vpython.org/contents/download/visual-5.32_release.tar.bz2 idlepath.patch)
md5sums=('866f51f0c49b60086dbe3581f508d30b'
         '565dfeb03de806ba2382d05c0ded27d6')

build() {
  cd ${srcdir}/visual-${pkgver}_release
  patch -Np1 -i ${srcdir}/idlepath.patch || return 1
  pythondir=`python -c "from distutils import sysconfig; print sysconfig.get_python_lib()"`
  unset PYTHONPATH # ignore developers PYTHONPATH
  ./configure --prefix=/usr \
              --with-example-dir=$pythondir/visual/demos
  make || return 1
}

package() {
  cd ${srcdir}/visual-${pkgver}_release
  make DESTDIR=${pkgdir} install
  install -D -m644 license.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

