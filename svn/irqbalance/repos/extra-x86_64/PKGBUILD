# Maintainer: Dan McGee <dan@archlinux.org>
# Contributor: Martin Striz <ms@poruba.net>

pkgname=irqbalance
pkgver=0.56
pkgrel=1
pkgdesc="IRQ balancing daemon for SMP systems"
arch=('i686' 'x86_64')
url="http://www.irqbalance.org/"
license=('GPL')
depends=(glib2)
makedepends=(pkgconfig)
backup=(etc/conf.d/irqbalance)
source=(http://irqbalance.googlecode.com/files/irqbalance-$pkgver.tbz2
        irqbalance.conf.d
        irqbalance.rc.d)
md5sums=('cd0c4d3b2bb84778a04fc594ad83949a'
         '336c1ee99818f9ecda1687e34c69fd6b'
         'fb82fc5d267d39110baf720d81282a7c')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./autogen.sh
  ./configure
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m755 irqbalance $pkgdir/usr/sbin/irqbalance
  install -D -m644 irqbalance.1 $pkgdir/usr/share/man/man1/irqbalance.1
  install -D -m644 ../irqbalance.conf.d $pkgdir/etc/conf.d/irqbalance
  install -D -m755 ../irqbalance.rc.d $pkgdir/etc/rc.d/irqbalance
}
