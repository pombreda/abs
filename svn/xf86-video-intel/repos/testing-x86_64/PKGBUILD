# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=xf86-video-intel
pkgver=2.11.901
pkgrel=1
pkgdesc="X.org Intel i810/i830/i915/945G/G965+ video drivers"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('intel-dri>=7.8' 'libpciaccess>=0.10.9' 'libdrm>=2.4.20' 'libxvmc>=1.0.5' 'libxcb>=1.5' 'xcb-util>=0.3.6')
makedepends=('pkgconfig' 'xorg-server>=1.8.0' 'xf86driproto>=2.1.0' 'glproto>=1.4.11' 'mesa>=7.8' 'xineramaproto>=1.2' 'inputproto')
conflicts=('xorg-server<1.8.0' 'xf86-video-i810' 'xf86-video-intel-legacy')
options=('!libtool')
groups=('xorg-video-drivers')
source=(${url}/releases/individual/driver/${pkgname}-${pkgver}.tar.bz2
        intel-2.11-no-pageflipping.patch
        LICENSE)
md5sums=('03b989f0a910c3a5966cbcf7c059a8ef'
         'dfc9f59fe048b412726c87fe3d45f4f5'
         'b924480931774dec14515e270db4924d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/intel-2.11-no-pageflipping.patch" || return 1
  ./configure --prefix=/usr --enable-dri || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/" || return 1
}
