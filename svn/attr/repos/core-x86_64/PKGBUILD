# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>
pkgname=attr
pkgver=2.4.44
pkgrel=1
pkgdesc="Extended attribute support library for ACL support"
arch=(i686 x86_64)
url="http://oss.sgi.com/projects/xfs/"
license=('LGPL')
depends=('glibc') 
makedepends=('gettext')
replaces=('xfsattr')
provides=('xfsattr')
conflicts=('xfsattr')
options=('!libtool')
source=(ftp://ftp.archlinux.org/other/attr/attr-${pkgver}.src.tar.gz)
sha256sums=('e01fa00686072a02dddf994e6217be950f51c1dc0852ad2e210c48b6e8de793a')

build() {
  cd $srcdir/attr-$pkgver 
  autoconf
  ./configure --prefix=/usr 
  sed -i -e 's/man2//g' man/Makefile
  make || return 1 
  make prefix=${pkgdir}/usr install install-lib install-dev

  # tidy up
  cd "${pkgdir}"

  mkdir -v lib
  mv -v usr/lib/libattr.so* lib/
  ln -sv ../../lib/libattr.so.1 usr/lib/libattr.so

  mv -v usr/libexec/libattr.{a,la} usr/lib/
  rm -rvf usr/libexec
}
