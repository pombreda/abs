# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>

pkgname=m4
pkgver=1.4.14
pkgrel=2
pkgdesc="The GNU macro processor"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/m4"
license=('GPL3')
groups=('base-devel')
depends=('glibc' 'bash')
install=m4.install
source=(ftp://ftp.gnu.org/gnu/m4/$pkgname-$pkgver.tar.xz
        m4-1.4.14-stat_h.patch)
md5sums=('e2b871099546846d1df586f195f79b53'
         'ccb7b698bdade1ae38c17f8e9d7cd3bc')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  patch -Np1 -i $srcdir/m4-1.4.14-stat_h.patch
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make prefix=${pkgdir}/usr install
}
