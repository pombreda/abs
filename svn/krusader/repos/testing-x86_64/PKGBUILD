# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>

pkgname=krusader
pkgver=2.2.0beta1
_pkgver=2.2.0-beta1
pkgrel=1
pkgdesc="An alternative file manager for KDE resembling Midnight Commander"
url="http://krusader.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('kdebase-lib')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${_pkgver}.tar.bz2")
md5sums=('71120ee8a188a12f879a6f1330b6b472')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${_pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install || return 1
}
