# $Id$
# Maintainer: firmicus <francois@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: herb <hrose@archlinux.org>

pkgname=perl-xml-parser
_realname=XML-Parser
pkgver=2.36
pkgrel=7
pkgdesc="Expat-based XML parser module for perl"
arch=(i686 x86_64)
license=('GPL' 'PerlArtistic')
url="http://search.cpan.org/dist/${_realname}/"
depends=('perl' 'expat')
replaces=('perlxml')
provides=("perlxml=${pkgver}")
options=(!emptydirs)
source=(http://search.cpan.org/CPAN/authors/id/M/MS/MSERGEANT/${_realname}-${pkgver}.tar.gz)
md5sums=('1b868962b658bd87e1563ecd56498ded')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  
  # install module in vendor directories.
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  make install DESTDIR="${pkgdir}"

  # remove perllocal.pod and .packlist.
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
