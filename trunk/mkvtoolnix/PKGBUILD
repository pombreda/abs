# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: 03/08/04 <lefungus@altern.org>

pkgname=mkvtoolnix
pkgver=4.0.0
pkgrel=2
pkgdesc="Set of tools to create, edit and inspect Matroska files"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.bunkus.org/videotools/mkvtoolnix/index.html"
depends=('libmatroska' 'flac' 'libvorbis' 'file' 'boost-libs' 'lzo2')
makedepends=('wxgtk' 'boost')
optdepends=('wxgtk: mkvmerge,mkvinfo and mmg GUI')
source=("http://www.bunkus.org/videotools/${pkgname}/sources/${pkgname}-${pkgver}.tar.bz2")
md5sums=('434eb24b9c49a99ac386bd2c4c525538')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr \
        --with-boost-libdir=/usr/lib || return 1
    make || return 1
}

package () {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install || return 1
}
