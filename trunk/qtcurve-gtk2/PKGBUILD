# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=qtcurve-gtk2
pkgver=1.5.0
pkgrel=1
arch=('i686' 'x86_64')
groups=('qtcurve')
license=('GPL')
pkgdesc='A configurable set of widget styles for KDE and Gtk'
url='http://www.kde-look.org/content/show.php?content=40492'
source=("http://craigd.wikispaces.com/file/view/QtCurve-Gtk2-${pkgver}.tar.bz2")
depends=('gtk2')
makedepends=('pkgconfig' 'cmake')
options=('!libtool')
md5sums=('92be23be72c6c28d74ee9a462771004b')

build() {
	cd $srcdir
	mkdir build
	cd build
	cmake ../QtCurve-Gtk2-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make || return 1
}

package() {
	cd $srcdir/build
	make DESTDIR=$pkgdir install || return 1
}
