# Maintainer: Firmicus <firmicus ατ gmx δοτ net>

pkgname=texlive-core
pkgver=2010.19146
pkgrel=1
pkgdesc="TeX Live core distribution"
license=('GPL')
arch=(any)
depends=('texlive-bin')
optdepends=('perl' 'ruby' 'ghostscript' 'psutils' 't1utils')
groups=('texlive-most')
conflicts=('tetex')
provides=('tetex')
replaces=('tetex')
url='http://tug.org/texlive/'
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip" "$pkgname.maps")
options=(!strip)
install=texlive.install
backup=(etc/texmf/tex/context/config/cont-usr.tex)
md5sums=('cd2e3304beb7b48e8e7acf1246628f3c'
         'f27c9370a4fe7c9fe7d746da7fbc37c0')

build() {
   echo -n "   --> extracting all packages... "
   for p in *.tar.xz; do 
     bsdtar -xf $p || return 1
   done
   echo "done"
   rm -rf {tlpkg,doc,source,omega} || true
}
package() {
   install -m755 -d $pkgdir/var/lib/texmf/arch/installedpkgs || return 1
   sed -i '/^#/d' CONTENTS || return 1
   _revnr=`echo $pkgver | sed 's/2010\.//'`
   install -m644 CONTENTS $pkgdir/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs || return 1
   install -m644 $pkgname.maps $pkgdir/var/lib/texmf/arch/installedpkgs/ || return 1
   install -m755 -d $pkgdir/usr/share || return 1
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \; || return 1
     find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \; || return 1
   done
   find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \; || return 1
   find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \; || return 1
   if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
     find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi

   # copy config file to texmf-config
   mkdir -p $pkgdir/etc/texmf/tex/context/config
   cp -a $pkgdir/usr/share/texmf-dist/tex/context/config/cont-usr.tex \
  	  $pkgdir/etc/texmf/tex/context/config/cont-usr.tex
   
   # remove obsolete stuff (we don't provide the omega and aleph binaries anyway)
   #rm -rf $pkgdir/usr/share/texmf-dist/omega
   rm -f $pkgdir/usr/share/texmf-dist/tex/plain/config/{omega,aleph}.ini
   # more cleanup
   rm -rf $pkgdir/usr/share/texmf-dist/scripts/context/stubs/mswin/
}
