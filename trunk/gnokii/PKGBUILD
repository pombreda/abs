# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=gnokii
pkgver=0.6.29
pkgrel=1
pkgdesc="Tools and user space driver for use with mobile phones"
arch=('i686' 'x86_64')
url="http://www.gnokii.org/"
license=('GPL')
depends=('libusb' 'libxpm' 'bluez' 'libical' 'postgresql-libs')
makedepends=('gtk2' 'libmysqlclient' 'perlxml' 'intltool')
optdepends=('gtk2: xgnokii GUI'
            'libmysqlclient: smsd mysql backend'
	    'postgresql-libs: smsd postgresql backend')
backup=('etc/gnokiirc')
options=('!libtool' '!makeflags')
source=(http://www.gnokii.org/download/$pkgname/$pkgname-${pkgver}.tar.bz2
	'gnokii.patch')
md5sums=('6111e0158a1129062bda6420db67c313'
         '4d764727686ee34bc73489c8b1321c82')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # FS#13249
  sed -i 's|cellphone|phone|' xgnokii/xgnokii.desktop.in

  patch -Np1 -i "${srcdir}/gnokii.patch"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --enable-security \
    --disable-unix98test
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 Docs/sample/gnokiirc "${pkgdir}/etc/gnokiirc"
}
