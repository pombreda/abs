# Maintainer: francois <francois.archlinux.org>

pkgname=texlive-bin
pkgver=2010.0
pkgrel=1
pkgdesc="TeX Live binaries"
license=('GPL')
arch=('i686' 'x86_64')
depends=('t1lib' 'gd' 'libgraphite' 'icu' 'poppler' 'libsigsegv')
makedepends=('perl' 'clisp' 'ffcall')
provides=('lcdf-typetools' 'kpathsea' 'xindy')
options=('!makeflags' '!libtool')
url='http://tug.org/texlive/'
source=('texmf.cnf'
        'fix-fontforge-encoding.patch'
        'ftp://ftp.archlinux.org/other/texlive/texlive-bin-source-20100623.tar.xz'
        'ftp://ftp.archlinux.org/other/texlive/texlive-bin-texmf-20100623.tar.xz')
backup=(etc/texmf/web2c/texmf.cnf \
  etc/texmf/chktex/chktexrc \
	etc/texmf/web2c/mktex.cnf \
	etc/texmf/web2c/updmap.cfg \
	etc/texmf/web2c/fmtutil.cnf \
	etc/texmf/tex/generic/config/language.dat \
	etc/texmf/tex/generic/config/language.def \
	etc/texmf/tex/generic/config/pdftexconfig.tex \
	etc/texmf/ttf2pk/ttf2pk.cfg \
	etc/texmf/dvips/config/config.ps \
	etc/texmf/dvipdfmx/dvipdfmx.cfg \
	etc/texmf/dvipdfm/config/config \
	etc/texmf/xdvi/XDvi)

build() {
   if [ "${CARCH}" = "x86_64" ]; then
     export CFLAGS="${CFLAGS} -fPIC"
     export CXXFLAGS="${CXXFLAGS} -fPIC"
   fi

   cd $srcdir
   # this patch removes spurious error message with locale "xx_YY.utf8"
   patch -p0 -i fix-fontforge-encoding.patch || return 1

   #############################################################
   ### configure
   cd source
   ## prevent compiling Xdvi with libXp
   sed -i~ 's|-lXp ||' texk/xdvik/configure
   test ! -d Work && mkdir Work
   cd Work
   echo "--> Initial configuration..."
   # we use temporary prefix to avoid messing the existing $pkgdir/usr/share/texmf tree
   ../configure --prefix=/usr \
     --datarootdir=$srcdir/inst/usr/share \
     --datadir=$srcdir/inst/usr/share \
     --mandir=$srcdir/inst/usr/share/man \
     --disable-native-texlive-build \
     --with-tex-banner="TeX Live 2010/Arch Linux" \
     --disable-multiplatform \
     --disable-dialog \
     --disable-psutils \
     --disable-t1utils \
     --disable-xz \
     --with-system-icu \
     --with-system-zlib \
     --with-system-pnglib \
     --with-system-ncurses \
     --with-system-t1lib \
     --with-system-gd \
     --with-system-xpdf \
     --with-system-freetype2 \
     --with-system-graphite \
     --with-freetype2-libdir=/usr/lib \
     --with-freetype2-include=/usr/include/freetype2 \
     --with-xdvi-x-toolkit=xaw \
     --disable-dump-share \
     --disable-aleph \
     --enable-xindy --disable-xindy-rules --disable-xindy-docs || return 1
   #############################################################
   ### make
   echo "-------------------------------------------------------"
   echo "--> Building the whole beast ..."
   echo "-------------------------------------------------------"
   make || return 1
}

package() {
   cd $srcdir
   #############################################################
   ### install texmf tree
   echo "-------------------------------------------------------"
   echo "--> installing the texmf tree"
   echo "-------------------------------------------------------"
   install -m755 -d $pkgdir/usr/share || return 1
   cp -r texmf $pkgdir/usr/share/ || return 1
   install -d -m755 $pkgdir/etc/texmf/web2c
   install -d -m755 $pkgdir/etc/texmf/chktex
   install -d -m755 $pkgdir/etc/texmf/dvips/config
   install -d -m755 $pkgdir/etc/texmf/dvipdfm/config
   install -d -m755 $pkgdir/etc/texmf/dvipdfmx
   install -d -m755 $pkgdir/etc/texmf/tex/generic/config
   install -d -m755 $pkgdir/etc/texmf/ttf2pk
   install -d -m755 $pkgdir/etc/texmf/xdvi
   # replace upstream texmf.cnf with ours
   rm -f $pkgdir/usr/share/texmf/web2c/texmf.cnf
   install -m644 $srcdir/texmf.cnf $pkgdir/etc/texmf/web2c/texmf.cnf || return 1
   # since the location of texmf.cnf is hard-wired to be under /usr/share/texmf/web2c
   # we make a symlink from /etc/texmf/web2c/texmf.cnf to the latter
   ln -s /etc/texmf/web2c/texmf.cnf $pkgdir/usr/share/texmf/web2c/texmf.cnf || return 1
   ## remove aleph from fmtutil.cnf
   sed -i -e '/aleph/d' $pkgdir/usr/share/texmf/web2c/fmtutil.cnf || return 1
   # move man files to /usr/share/man
   for i in 1 5; do
     install -d -m755 $pkgdir/usr/share/man/man$i
     mv $pkgdir/usr/share/texmf/doc/man/man$i/*.$i $pkgdir/usr/share/man/man$i/
   done
   # move info files to /usr/share/info
   install -d -m755 $pkgdir/usr/share/info
   mv $pkgdir/usr/share/texmf/doc/info/*.info $pkgdir/usr/share/info/
   rm -rf $pkgdir/usr/share/texmf/doc/{man,info}
   # move xindy modules to /usr/share/xindy
   mv $pkgdir/usr/share/texmf/xindy $pkgdir/usr/share/
   # copy config files to $TEXMFCONFIG tree
   cp -a $pkgdir/usr/share/texmf/chktex/chktexrc \
     	$pkgdir/etc/texmf/chktex/
   cp -a $pkgdir/usr/share/texmf/web2c/mktex.cnf \
   	  $pkgdir/etc/texmf/web2c/
   cp -a $pkgdir/usr/share/texmf/web2c/updmap.cfg \
   	  $pkgdir/etc/texmf/web2c/
   cp -a $pkgdir/usr/share/texmf/web2c/fmtutil.cnf \
   	  $pkgdir/etc/texmf/web2c/
   cp -a $pkgdir/usr/share/texmf/dvips/config/config.ps \
   	  $pkgdir/etc/texmf/dvips/config/
   cp -a $pkgdir/usr/share/texmf/dvipdfm/config/config \
   	  $pkgdir/etc/texmf/dvipdfm/config/
   cp -a $pkgdir/usr/share/texmf/dvipdfmx/dvipdfmx.cfg \
   	  $pkgdir/etc/texmf/dvipdfmx/
   cp -a $pkgdir/usr/share/texmf/tex/generic/config/pdftexconfig.tex \
   	  $pkgdir/etc/texmf/tex/generic/config/
   cp -a $pkgdir/usr/share/texmf/tex/generic/config/language.dat \
   	  $pkgdir/etc/texmf/tex/generic/config/
   cp -a $pkgdir/usr/share/texmf/tex/generic/config/language.def \
   	  $pkgdir/etc/texmf/tex/generic/config/
   cp -a $pkgdir/usr/share/texmf/ttf2pk/ttf2pk.cfg \
   	  $pkgdir/etc/texmf/ttf2pk/
   cp -a $pkgdir/usr/share/texmf/xdvi/XDvi \
   	  $pkgdir/etc/texmf/xdvi/
   # remove TL specific warnings in the language.{dat,def} files:
   sed -i -e '/DO NOT EDIT/,+3 d' $pkgdir/etc/texmf/tex/generic/config/language.*
   # clean updmap.cfg
   sed -i '/^\(Map\|MixedMap\)/d' $pkgdir/etc/texmf/web2c/updmap.cfg
   sed -i '/^#! \(Map\|MixedMap\)/d' $pkgdir/etc/texmf/web2c/updmap.cfg

   #############################################################
   ### install
   cd source
   # fixes for xindy
   find utils/xindy -name Makefile -exec sed -i -e "s|^prefix =.\+$|prefix = $pkgdir/usr|" -e "s|^mandir =.\+$|mandir = \${prefix}/share/man|" -e "s|^datadir =.\+$|datadir = \${datarootdir}/texmf|" -e "s|^docdir =.\+$|docdir = \${datadir}/doc/xindy|" '{}' \; || return 1

   echo "-------------------------------------------------------"
   echo "--> Proceeding with make install ..."
   echo "-------------------------------------------------------"
   cd Work
   make prefix=$srcdir/inst/usr texmf=$pkgdir/usr/share/texmf install || return 1

   # restore install location of bin/lib/include
   mv -f $srcdir/inst/usr/bin $pkgdir/usr/bin || return 1
   mv -f $srcdir/inst/usr/lib $pkgdir/usr/lib || return 1
   mv -f $srcdir/inst/usr/include $pkgdir/usr/include || return 1
   # add symlinks to manpages since they are not included in the original texmf tree
   for m in $srcdir/inst/usr/share/man/man1/*; do
     bm=$(basename $m)
     test -f $pkgdir/usr/share/man/man1/$bm || mv -f $m $pkgdir/usr/share/man/man1/
   done

   # symlinks in $pkgdir/usr/bin are incorrect
   echo "--> Fixing symlinks for scripts ..."
   for f in $pkgdir/usr/bin/* ; do
	   if [ -L $f ]; then
		   target=`ls -l "$f" | sed 's/^.\+ -> //'`
		   if [[ "$target" == ..* ]]; then
			   newtarget=`echo $target | sed -e 's#../#/usr/share/#'`
			   rm -f $f
			   ln -s $newtarget $f
			   test -f $pkgdir/$newtarget && chmod a+x $pkgdir/$newtarget
		   fi
	   fi
   done

   # create symlinks for formats 
   echo "--> Create symlinks for TeX formats ..."
   PATH="$PATH:${pkgdir}/usr/bin" texlinks -f $pkgdir/usr/share/texmf/web2c/fmtutil.cnf $pkgdir/usr/bin/ || return 1
   #############################################################
   # remove symlinks to scripts that are not in texlive-bin or texlive-core:
   SYMLINKS_TO_REMOVE="authorindex \
ebong \
bibexport \
cachepic \
epspdf \
epspdftk \
epstopdf \
fig4latex \
makeglossaries \
mathspic \
mkgrkindex \
pdfannotextractor \
perltex \
pdfthumb \
ppower4 \
pst2pdf \
ps4pdf \
splitindex \
svn-multi \
htcontext \
htlatex \
htmex \
ht \
httexi \
httex \
htxelatex \
htxetex \
mk4ht \
ulqda \
vpe"
   for s in $SYMLINKS_TO_REMOVE; do
     rm -f $pkgdir/usr/bin/$s
   done
   ###################################################################
}

md5sums=('f848bb8e2a87ceef49cfd0eac6b140c9'
         'bfb9716aa00c86c08cd31e5b32edeb98'
         'e32fd2e192ec4cba148ed97c712d3c95'
         '758c144a68b788343462e050ca461a2f')
