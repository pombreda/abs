# $Id$
# Maintainer:
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>

pkgname=dia
pkgver=0.97.1
pkgrel=1
pkgdesc="A GTK+ based diagram creation program"
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/Dia"
install=dia.install
depends=('libxslt' 'desktop-file-utils' 'libart-lgpl' 'gtk2')
makedepends=('intltool' 'python' 'docbook-xsl')
options=('!libtool')
source=("ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.97/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('8dfe8b2c9d87baf29834c8de5e3ec91497c2b17f2b77fb1b867afddf5c429142')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --enable-db2html \
    --with-cairo \
    --with-python \
    --disable-gnome || return 1
  make || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
}
