# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=inkscape
pkgver=0.47
pkgrel=3
pkgdesc="A vector-based drawing program - svg compliant"
url="http://inkscape.sourceforge.net/"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
depends=('poppler-glib>=0.14.0' 'imagemagick>=6.6.2.0' 'gtkspell>=2.0.16' 'gc>=7.1' 'gtkmm>=2.20.3' 'libxslt>=1.1.26' 'gsl>=1.14' 'pyxml>=0.8.4' 'desktop-file-utils' 'popt')
makedepends=('boost' 'pkgconfig' 'intltool')
options=('!libtool')
install=inkscape.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
        50-poppler-API.dpatch
        inkscape-0.47-gcc45.patch)
md5sums=('916c85b501bcfb0ae61fcf7318f36348'
         'b447272c9b282b49e86dca24c2581065'
         'a63bb0a325e3c40d86420183e003350d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/50-poppler-API.dpatch"
  patch -Np1 -i "${srcdir}/inkscape-0.47-gcc45.patch"
  ./configure --prefix=/usr \
    --with-python \
    --with-perl \
    --without-gnome-vfs \
    --with-xft \
    --enable-lcms \
    --enable-poppler-cairo \
    --disable-dependency-tracking
  make
  make DESTDIR="${pkgdir}" install
}
