# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=obconf
pkgver=2.0.3
pkgrel=3
pkgdesc="A gtk2 based configuration tool for the Openbox windowmanager"
arch=('i686' 'x86_64')
url="http://icculus.org/openbox/index.php/ObConf:About"
license=('GPL')
depends=('openbox>=3.4.5' 'gtk2>=2.16.1' 'libglade' 'desktop-file-utils')
install=${pkgname}.install
source=(http://icculus.org/openbox/${pkgname}/${pkgname}-${pkgver}.tar.gz \
        http://icculus.org/openbox/mw/images/8/80/Obconf-72.png)
md5sums=('b22e273721851dedad72acbc77eefb68'
 	 '340ae4732e76731d2838473541eb8d4e')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure --prefix=/usr || return 1
	make || return 1
	sed -i 's/=openbox/=obconf/' obconf.desktop || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install || return 1
	install -Dm644 ../Obconf-72.png \
	    ${pkgdir}/usr/share/pixmaps/obconf.png || return 1
}
