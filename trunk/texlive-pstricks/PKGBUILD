# Maintainer: Firmicus <firmicus āt gmx dōt net>

pkgname=texlive-pstricks
pkgver=2010.19110
pkgrel=1
pkgdesc="TeX Live - Additional PSTricks packages"
license=('GPL')
arch=(any)
depends=('texlive-core')
groups=('texlive-most')
url='http://tug.org/texlive/'
source=("ftp://ftp.archlinux.org/other/texlive/$pkgname-$pkgver-src.zip")
options=('!emptydirs')
options=(!strip)
install=texlive.install
md5sums=('6aae3fe8e2cb61a9495e342794f9b6c3')

build() {
   for p in *.tar.xz; do
	   bsdtar -xf $p || return 1
   done
   rm -rf {tlpkg,doc,source} || true
}
package() {
   install -m755 -d $pkgdir/var/lib/texmf/arch/installedpkgs || return 1
   sed -i '/^#/d' CONTENTS || return 1
   _revnr=`echo $pkgver | sed 's/2010\.//'`
   install -m644 CONTENTS $pkgdir/var/lib/texmf/arch/installedpkgs/${pkgname}_${_revnr}.pkgs || return 1
   install -m755 -d $pkgdir/usr/share || return 1
   wanteddirs=$(for d in *; do test -d $d && [[ $d != texmf* ]] && echo $d; done) || true
   for dir in $wanteddirs; do
     find $dir -type d -exec install -d -m755 $pkgdir/usr/share/texmf-dist/'{}' \; || return 1
     find $dir -type f -exec install -m644 '{}' $pkgdir/usr/share/texmf-dist/'{}' \; || return 1
   done
   if [[ -d texmf-dist ]]; then
     find texmf-dist -type d -exec install -d -m755 $pkgdir/usr/share/'{}' \; || return 1
     find texmf-dist -type f -exec install -m644 '{}' $pkgdir/usr/share/'{}' \; || return 1
   fi
   if [[ -d $pkgdir/usr/share/texmf-dist/scripts ]]; then
     find $pkgdir/usr/share/texmf-dist/scripts -type f -exec chmod a+x '{}' \;
   fi
   install -m755 -d $pkgdir/usr/bin || return 1
   ln -s /usr/share/texmf-dist/scripts/pst2pdf/pst2pdf $pkgdir/usr/bin/pst2pdf
}
