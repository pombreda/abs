# $Id$
# Maintainer: Paul Mattal <paul@archlinux.org>

pkgname=mailman
pkgver=2.1.12
pkgrel=1
pkgdesc="Mailing list manager with built in web access"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.list.org/"
depends=('python' 'apache' 'smtp-server')
# 'Defaults.py' should not be changed by users; 'mm_cfg.py' should instead.
backup=('usr/lib/mailman/Mailman/mm_cfg.py')
install=$pkgname.install
source=(http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tgz \
	rc.mailman)
md5sums=('d565a6d2d0ec6d2dd6936a81e1c1ca86'
         '3d83d06d0ec3319bf3c7d9df5d18e89f')

build() {
  cd $srcdir/$pkgname-$pkgver || return 1
  
  # the mailman user and group are required to build
  if [ ! `egrep '^mailman' /etc/passwd` ]; then
    msg "Adding user/group mailman (temporarily)"
    groupadd -g 80 mailman || return 1
    sleep 2
    useradd -u 80 -g mailman -d /home/mailman -s /bin/false mailman \
    	|| return 1
    mkdir -p /home/mailman || return 1
    chown mailman.mailman /home/mailman || return 1
    chmod a+rx,g+ws /home/mailman || return 1
    cleanup=1
  else
    cleanup=0
  fi

  # set permissions and ownership on the target directory
  #mkdir -p $pkgdir/home/mailman || return 1
  #chown mailman.mailman $pkgdir/home/mailman || return 1
  #chmod a+rx,g+ws $pkgdir/home/mailman || return 1

  # configure and build
  ./configure --without-permcheck \
              --prefix=/usr/lib/mailman \
              --with-var-prefix=/var/lib/mailman \
              --with-mail-gid=mailman \
              --with-cgi-gid=http

  make || return 1
  make DESTDIR=$pkgdir install || return 1

  # install the launch script
  install -D -m755 $srcdir/rc.mailman $pkgdir/etc/rc.d/mailman || return 1

  if [ $cleanup -eq 1 ]; then
    msg "Removing user/group mailman"
    userdel mailman
    rmdir /home/mailman
  fi
}
