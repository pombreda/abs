# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Jason Chu <jason@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=cscope
pkgver=15.7a
pkgrel=2
pkgdesc="A developers tool for browsing program code"
arch=('i686' 'x86_64')
url="http://cscope.sourceforge.net/index.html"
license=('BSD')
depends=('ncurses' 'sh')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('da43987622ace8c36bbf14c15a350ec1')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}
	sed -i 's|/usr/local/lib/cs|/usr/lib/cs|' contrib/ocs || return 1

	./configure --prefix=/usr || return 1
	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}
	make DESTDIR=${pkgdir} install || return 1
  
	install -d ${pkgdir}/usr/lib/cs

	cd contrib/xcscope
	install -Dm644 xcscope.el \
		${pkgdir}/usr/share/emacs/site-lisp/xcscope.el || return 1
	install -m755 cscope-indexer ${pkgdir}/usr/bin || return 1

	install -Dm644 ${srcdir}/${pkgname}-${pkgver}/COPYING \
		${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
