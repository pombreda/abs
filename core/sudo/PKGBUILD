# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=sudo
pkgver=1.7.2p7
pkgrel=2
pkgdesc="Give certain users the ability to run some commands as root"
arch=('i686' 'x86_64')
url="http://www.sudo.ws/sudo/"
license=('custom')
depends=('glibc' 'pam')
backup=('etc/sudoers' 'etc/pam.d/sudo')
source=(ftp://ftp.sudo.ws/pub/sudo/$pkgname-$pkgver.tar.gz
        pam_auth.diff
        sudo.pam)
options=('!libtool' '!makeflags' 'force')
md5sums=('3ac78668427a53e12d7639fdfab2f1af'
         'a9aac5f2928f858b83720d914ee9c9ab'
         '4e7ad4ec8f2fe6a40e12bcb2c0b256e3')

build() {
  cd $srcdir/$pkgname-$pkgver

  # http://www.gratisoft.us/bugzilla/show_bug.cgi?id=388
  patch -Np0 -i $srcdir/pam_auth.diff

  ./configure --prefix=/usr --with-pam --libexecdir=/usr/lib \
    --with-env-editor --with-all-insults --with-logfac=auth
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install
  install -Dm644 $srcdir/sudo.pam $pkgdir/etc/pam.d/sudo

  install -Dm644 LICENSE $pkgdir/usr/share/licenses/sudo/LICENSE
}
