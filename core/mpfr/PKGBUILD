# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=mpfr
pkgver=3.0.0
pkgrel=1
pkgdesc="Multiple-precision floating-point library"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://www.mpfr.org/"
depends=('gmp>=5.0')
options=('!libtool')
install=mpfr.install
source=(http://www.mpfr.org/mpfr-current/mpfr-${pkgver}.tar.xz)
md5sums=('8ab3bef2864b8c6e6a291f5603141bbd')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-thread-safe --enable-shared || return 1
  make || return 1
  make check || return 1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install || return 1
}
